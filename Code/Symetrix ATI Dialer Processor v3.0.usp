/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name:
System Number:
Programmer:MK
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
// #ENABLE_DYNAMIC
#SYMBOL_NAME "Symetrix ATI Dialer Processor v3.0"
// #HINT ""
#DEFINE_CONSTANT ciMaxInstances 50
#DEFINE_CONSTANT ciMaxCharacters 100
#DEFINE_CONSTANT ciBufferSize 5000
#DEFINE_CONSTANT cbDebug 0

#CATEGORY "34" // Mixer
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
// #ENCODING_ASCII
// #ENCODING_UTF16
// #ENCODING_INHERIT_FROM_PARENT
// #ENCODING_INHERIT_FROM_PROGRAM

#HELP_BEGIN
To be used only within Symetrix ATI Dialer module
#HELP_END


/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)
*******************************************************************************************/
// #CRESTRON_LIBRARY ""
// #USER_LIBRARY ""

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT CS_Dial;
// ANALOG_INPUT 
STRING_INPUT CS_Dial_String[31];
BUFFER_INPUT From_Command_Processor[ciBufferSize];
 
DIGITAL_OUTPUT CS_Dial_String_is_Empty;
STRING_OUTPUT To_Command_Processor, Dialed_Number, Caller_ID;
// ANALOG_OUTPUT

/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/
// TCP_CLIENT
// TCP_SERVER
// UDP_SOCKET

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
INTEGER_PARAMETER _SKIP_, _SKIP_, _SKIP_, _SKIP_;
INTEGER_PARAMETER Unit_ID, Card_ID, Line_Number, Speed_Preset_For_CS_Dial;
// SIGNED_INTEGER_PARAMETER
// LONG_INTEGER_PARAMETER
// SIGNED_LONG_INTEGER_PARAMETER
STRING_PARAMETER Control_ID[ciMaxInstances][5];

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
/*
#BEGIN_PARAMETER_PROPERTIES parameter_variable, parameter_variable, ...
   // propValidUnits = // unitString or unitDecimal|unitHex|unitPercent|unitCharacter|unitTime|unitTicks;
   // propDefaultUnit = // unitString, unitDecimal, unitHex, unitPercent, unitCharacter, unitTime or unitTicks;
   // propBounds = lower_bound , upper_bound;
   // propDefaultValue = ;  // or, propDefaultValue = "";
   // propList = // { "value" , "label" } , { "value" , "label" } , ... ;
   // propShortDescription = "status_bar_hint_text";
   // #BEGIN_PROP_FULL_DESCRIPTION  line_1...  line_2...  line_n  #END_PROP_FULL_DESCRIPTION
   // #BEGIN_PROP_NOTES line_1...  line_2...  line_n  #END_PROP_NOTES
#END_PARAMETER_PROPERTIES
*/

/*******************************************************************************************
  Structure Definitions
  (Uncomment and define structure definitions as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: struct.myString = "";
*******************************************************************************************/
/*
STRUCTURE MyStruct1
{
};

MyStruct1 struct;
*/

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
INTEGER	bFlag1, iTemp, iTempValue, a;
INTEGER iControlIDLow, iControlIDHi, iTempControlID, iInstancesUsed;
STRING	sTempDevice[ciMaxCharacters], sTemp[ciMaxCharacters];

INTEGER	iTempUnit, iTempResource, iTempEnum, iTempCard, iTempChannel;
STRING	sPhoneData[ciMaxCharacters];

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
Function ProcessDeviceMsg()
{
	if (cbDebug)
		Trace("%s sTempDevice=%s\n", GetSymbolInstanceName(), sTempDevice);
    if (find("Send Info\r", sTempDevice))
    {
    	makestring(To_Command_Processor, "%u Send Info 1 %u %u %u %u %u\r", atoi(sTempDevice), iControlIDLow, iControlIDHi, Unit_ID, Card_ID, Line_Number);
    }
	else if (find("SSYS", sTempDevice) = 1)//Phone String Response
	{
		if (cbDebug)
			Trace("%s Find Phone String Response\n", GetSymbolInstanceName());
       	sTemp = remove("SSYSS ", sTempDevice);
       	iTempUnit = atoi(sTempDevice);
       	sTemp = remove(".", sTempDevice);
       	iTempResource = atoi(sTempDevice);
       	sTemp = remove(".", sTempDevice);
       	iTempEnum = atoi(sTempDevice);
       	sTemp = remove(".", sTempDevice);
       	iTempCard = atoi(sTempDevice);
       	sTemp = remove(".", sTempDevice);
       	iTempChannel = atoi(sTempDevice);
       	sTemp = remove("=", sTempDevice);
       	sPhoneData = left(sTempDevice, len(sTempDevice)-1);
   		if (cbDebug)
   			Trace("%s iTempUnit=%u, iTempResource=%u, iTempEnum=%u, iTempCard=%u, iTempChannel=%u, sPhoneData=%s\n", GetSymbolInstanceName(), iTempUnit, iTempResource, iTempEnum, iTempCard, iTempChannel, sPhoneData);
   		if (iTempUnit = Unit_ID && iTempCard = Card_ID && iTempChannel = Line_Number)
   		{
   			if (iTempResource = 1002)//Dialed Number
   			{
   				Dialed_Number = sPhoneData;
   			}
   			else if (iTempResource = 1003)//Caller ID
   			{
   				Caller_ID = sPhoneData;
   			}
   		}

	}

	sTempDevice = "";
}

#if_series3
THREADSAFE Change From_Command_Processor
{
	while(1)
	{
		try
		{
			sTempDevice = gather("\x0D", From_Command_Processor);
			ProcessDeviceMsg();
		}
		catch
		{
			print("%s Issue with Device message handling\n", GetSymbolInstanceName());
		}
	}
}

#else
CHANGE From_Command_Processor
{
    if (bFlag1 = 0)
    {
    	bFlag1 = 1;
    	while(1)
    	{
			sTempDevice = gather("\x0D", From_Command_Processor);
			ProcessDeviceMsg();
		}
    	bFlag1 = 0;
    }
}

#endif

Push CS_Dial
{
 	makestring(To_Command_Processor, "SSYSS %u.1000.%u.%u.0=%s\r", Unit_ID, (Speed_Preset_For_CS_Dial-1), Card_ID, CS_Dial_String);
}

Change CS_Dial_String
{
 	if (len(CS_Dial_String) > 0)
 	{
 		CS_Dial_String_is_Empty = 0;
 	}
 	else
 	{
 		CS_Dial_String_is_Empty = 1;
 	}
}
/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
{
	sTempDevice = "";
	bFlag1 = 0;
	iInstancesUsed = 0;
	iControlIDLow = 20000;
	iControlIDHi = 0;
	CS_Dial_String_is_Empty = 1;
	for (a=1 to ciMaxInstances)
	{
		iTempControlID = atoi(Control_ID[a]);
		if (iTempControlID > 0 && iTempControlID <= 10000)
		{
			iInstancesUsed = iInstancesUsed + 1;
			if (iTempControlID < iControlIDLow)
			{
				iControlIDLow = iTempControlID;
			}
			if (iTempControlID > iControlIDHi)
			{
				iControlIDHi = iTempControlID;
			}
		}
		else
		{
			if (cbDebug)
				Trace("%s iControlID[%u]=%u invalid. Ignoring.\n", GetSymbolInstanceName(), a, iTempControlID);
		}
	}
	if (cbDebug)
		Trace("%s iControlIDLow=%u, iControlIDHi=%u, iInstancesUsed=%u\n", GetSymbolInstanceName(), iControlIDLow, iControlIDHi, iInstancesUsed);
	if (iInstancesUsed <> (iControlIDHi - iControlIDLow + 1))
	{
		Trace("%s Controller values are non-sequential\n", GetSymbolInstanceName());
	}
}
